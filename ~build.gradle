
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.20'
        classpath 'gradle.plugin.de.solugo.gradle:gradle-nodejs-plugin:0.2.1'
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'de.solugo.gradle.nodejs'

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    implementation "ch.delconte.screeps-kotlin:screeps-kotlin-types:1.3.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "commonjs"
}

nodejs {
    version = '10.8.0'
    //rootPath = 'subpath'
}
task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

task runQunit(type: NodeJsTask, dependsOn: [compileTestKotlin2Js, populateNodeModules]) {
    require = ["qunit", "kotlin","kotlin-test"]
    executable = "node_modules/qunit/bin/qunit"
    args = [projectDir.toPath().relativize(file(compileTestKotlin2Js.outputFile).toPath())]
}

test.dependsOn runQunit